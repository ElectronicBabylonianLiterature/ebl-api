%import common.CR
%import common.LF
%import common.WS_INLINE
%import .ebl_atf_text_line (text_line, any_word, note_line, parallel_line, translation_line, labels, manuscript_line, markup)
%import .ebl_atf_text_line (at_line)
%import .ebl_atf_common (DIGIT, INT, LCASE_LETTER)
%import .ebl_atf_common (free_text)
%import .ebl_atf_common (object, OBJECT, generic_object, fragment)
%import .ebl_atf_common (surface, SURFACE, generic_surface, face, edge)
%import .ebl_atf_common (seal)

?start: line

?line: empty_line
     | note_line
     | text_line
     | dollar_line
     | at_line
     | parallel_line
     | translation_line
     | control_line

empty_line: /\s+/?
!control_line.-2: ("=:" | "&" ) /.+/?

?paratext: note_line | dollar_line

chapter: chapter_line (_NEWLINE _NEWLINE+ chapter_line)*

chapter_line: [chapter_translation] line_variant (_NEWLINE line_variant)*
chapter_translation: (translation_line _NEWLINE)+

line_variant: reconstruction (_NEWLINE manuscript_line)*

reconstruction: text_line [_NEWLINE note_line] (_NEWLINE parallel_line)*

manuscript_line: _WHITE_SPACE? siglum  [" " labels] manuscript_text paratext_line*
?manuscript_text: " " text_line | empty_line
?paratext_line: _NEWLINE _WHITE_SPACE? paratext
_WHITE_SPACE: WS_INLINE
_NEWLINE: (CR? LF) 

siglum: [PROVENANCE] PERIOD [TYPE] [DISAMBIQUATOR]
      | "Std" [DISAMBIQUATOR] -> standard_text_siglum
PROVENANCE: "Assa"
         | "Abs"
         | "Adb"
         | "Ajr"
         | "Ala"
         | "Ama"
         | "Anh"
         | "Anš"
         | "Ašn"
         | "Ašš"
         | "Baba"
         | "Bab"
         | "Bakr"
         | "Baq"
         | "Bar"
         | "Bash"
         | "Bazm"
         | "Bitw"
         | "Bor"
         | "Btb"
         | "Chughā"
         | "Cut"
         | "Der"
         | "Dgr"
         | "Dha"
         | "Dil"
         | "Diqd"
         | "Dka"
         | "Dku"
         | "Dun"
         | "Ebl"
         | "Elam"
         | "Emr"
         | "Eri"
         | "Ešn"
         | "Fakhī"
         | "Fakh"
         | "Gar"
         | "Ghad"
         | "Gir"
         | "Gla"
         | "Grm"
         | "Guz"
         | "Had"
         | "Hafr"
         | "Haf"
         | "Ham"
         | "Har"
         | "Hat"
         | "Haw"
         | "Hur"
         | "Huz"
         | "Iba"
         | "Img"
         | "Iml"
         | "Irs"
         | "Isn"
         | "Jer"
         | "Jid"
         | "Kal"
         | "Kan"
         | "Kar"
         | "Kay"
         | "Khat"
         | "Kha"
         | "Kho"
         | "Kiš"
         | "Kmn"
         | "Ksr"
         | "Ktn"
         | "Kut"
         | "Lac"
         | "Lagab"
         | "Lag"
         | "Lah"
         | "Lar"
         | "Lrk"
         | "Mal"
         | "Mard"
         | "Mar"
         | "Maš"
         | "Meg"
         | "MetS"
         | "Met"
         | "Milm"
         | "Miz"
         | "Mrd"
         | "Muh"
         | "Muk"
         | "Nag"
         | "Nasr"
         | "Nēr"
         | "Nig"
         | "Nin"
         | "Nip"
         | "Nuzi"
         | "Paš"
         | "Per"
         | "Pik"
         | "Puz"
         | "Qat"
         | "Qaṭ"
         | "Šad"
         | "Šah"
         | "Sel"
         | "Shi"
         | "Shn"
         | "Sipam"
         | "Sip"
         | "Šub"
         | "Sul"
         | "Šur"
         | "Surj"
         | "Sus"
         | "Šuš"
         | "Tar"
         | "Ter"
         | "Tgo"
         | "Tikr"
         | "Tri"
         | "Ttb"
         | "Ttl"
         | "Tum"
         | "Uaaj"
         | "Ubd"
         | "Uga"
         | "Umm"
         | "Unc"
         | "Uqa"
         | "Urk"
         | "Ur"
         | "Waw"
         | "Ylk"
         | "Zab"
         | "Zar"
         | "Zaw"
PERIOD: "ED1_2"
      | "Fara"
      | "Hel"
      | "Hit"
      | "JN"
      | "LB"
      | "MA"
      | "MB"
      | "MElam"
      | "NA"
      | "NB"
      | "NElam"
      | "OA"
      | "OB"
      | "OElam"
      | "PElam"
      | "PSarg"
      | "Par"
      | "Per"
      | "Sarg"
      | "Unc"
      | "Ur3"
      | "Uruk4"
TYPE: "Sch"
    | "Com"
    | "Quo"
    | "Var"
    | "Par"
    | "Ex"
DISAMBIQUATOR: /[\S]+/

?dollar_line: "$" " "* value
?value: strict
      | "(" " "* strict " "* ")"
      | loose
?strict: state | ruling | image | seal
loose: "(" free_text ")"

state: QUALIFICATION [" " extent] [" " scope] [" " STATE] [" " DOLLAR_STATUS]
     | extent [" " scope] [" " STATE] [" " DOLLAR_STATUS] -> state_extent
     | scope [" " STATE] [" " DOLLAR_STATUS] -> state_scope
     | STATE [" " DOLLAR_STATUS] -> state_state
     | DOLLAR_STATUS -> state_status

QUALIFICATION : "at least" | "at most" | "about"

?extent: INT | range | EXTENT
EXTENT: "several" | "some" |  "rest of" | "start of" | "beginning of" | "middle of" | "end of"
range.2: INT " "? "-" " "? INT

?scope: SCOPE | object | surface
SCOPE: "columns" | "column" | "lines" | "line" | "cases" | "case" | "surface" | "side" | "excerpt"


STATE: "blank" | "broken" | "effaced" | "illegible" | "missing" | "traces"
     | "omitted" | "continues"

DOLLAR_STATUS: "!?" | "*" | "?" | "!" | "°"

ruling : RULING_NUMBER " ruling" [" "? DOLLAR_STATUS]
RULING_NUMBER : "single" | "double" | "triple"


image.1 : "(" " "? "image " INT [LCASE_LETTER] " = " free_text ")"
