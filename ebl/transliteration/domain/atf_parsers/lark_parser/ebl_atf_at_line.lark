%import common.INT
%import .ebl_atf_common (status, object, surface, seal, free_text)
%import .ebl_atf_note_line(_markup)

?at_line: "@" at_line_value

?at_line_value: seal | column | legacy_column | heading | discourse | object_with_status 
               | surface_with_status | divisions | composite

surface_with_status: surface " "? status

object_with_status: object " "? status

column: "column " INT " "? status

legacy_column : "column"

heading: "h" INT [ " " _markup ]

!discourse: "catchline" | "colophon" | "date" | "signature" | "signatures" | "summary" | "witnesses"

divisions: "m=division " free_text [" " INT]

?composite: composite_composite | composite_start | composite_end | composite_milestone
composite_start: "div " free_text [" " INT]
composite_end: "end " free_text
composite_composite: "composite"
composite_milestone: "m=locator " free_text [" " INT]
