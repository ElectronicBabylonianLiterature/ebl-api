%import common.DIGIT
%import common.INT
%import common.LCASE_LETTER
%import common.LETTER

%import .legacy_atf (LEGACY_VALUE_CHARACTER_ACCENTED, LEGACY_VALUE_CHARACTER)
%import .legacy_atf (LEGACY_LOGOGRAM_CHARACTER_ACCENTED)

seal : "seal "  INT

free_text.-2 : /./+ " "*

?object: OBJECT | generic_object | fragment
OBJECT: "tablet" | "envelope" | "prism" | "bulla"
fragment: "fragment " free_text
generic_object : "object " free_text

?surface: SURFACE | generic_surface | face | edge
SURFACE: "obverse" | "reverse" | "left" | "right" | "top" | "bottom"
face: "face " LCASE_LETTER
edge: "edge" (" " LCASE_LETTER)?
generic_surface : "surface " free_text

VALUE_CHARACTER_MAIN: "a" | "ā" | "â" | "b" | "d" | "e" 
      | "ē" | "ê" | "f" | "g" | "ĝ" | "h" 
      | "i" | "ī" | "î" | "y" | "k" | "l" 
      | "m" | "n" | "p" | "q" | "r" | "s" 
      | "ṣ" | "š" | "t" | "ṭ" | "u" | "ū"
      | "û" | "w" | "z" | "ḫ" | "ʾ"
LOGOGRAM_CHARACTER_MAIN: "A" | "Ā" | "Â" | "B" | "D" | "E" 
      | "Ē" | "Ê" | "G" | "Ĝ" | "H" | "I" 
      | "Ī" | "Î" | "Y" | "K" | "L" | "M" 
      | "N" | "P" | "Q" | "R" | "S" | "Ṣ" 
      | "Š" | "T" | "Ṭ" | "U" | "Ū" | "Û" 
      | "W" | "Z" | "Ḫ" | "ʾ"

VALUE_CHARACTER: VALUE_CHARACTER_MAIN | LEGACY_VALUE_CHARACTER_ACCENTED | LEGACY_VALUE_CHARACTER
LOGOGRAM_CHARACTER: LOGOGRAM_CHARACTER_MAIN | LEGACY_LOGOGRAM_CHARACTER_ACCENTED

GREEK_ALPHABET: "Α" | "α" | "Β" | "β" | "Γ" | "γ" | "Δ" | "δ" | "Ε" | "ε"
              | "Ζ" | "ζ" | "Η" | "η" | "Θ" | "θ" | "Ι" | "ι" | "Κ" | "κ"
              | "Λ" | "λ" | "Μ" | "μ" | "Ν" | "ν" | "Ξ" | "ξ" | "Ο" | "ο"
              | "Π" | "π" | "Ρ" | "ρ" | "Σ" | "σ" | "ς" | "Τ" | "τ" | "Υ"
              | "υ" | "Φ" | "φ" | "Χ" | "χ" | "Ψ" | "ψ" | "Ω" | "ω"

AKKADIAN_ALPHABET: "ʾ" | "A" | "B" | "D" | "E" | "G" | "H" | "I" | "K" | "L"
                 | "M" | "N" | "P" | "S" | "T" | "U" | "Y" | "Z" | "a" | "b"
                 | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "k" | "l" | "m"
                 | "n" | "p" | "q" | "r" | "s" | "t" | "u" | "w" | "y" | "z"
                 | "É" | "â" | "ê" | "î" | "û" | "ā" | "Ē" | "ē" | "ī" | "Š"
                 | "š" | "Ś" | "ś" | "ū" | "ṣ" | "ṭ" | "₄"

labels: surface_label " " column_label
      | surface_label
      | column_label

object_label: (TABLET | ENVELOPE | PRIMS | BULLA) status
TABLET: "tablet"
ENVELOPE: "envelope"
PRIMS: "prism"
BULLA: "bulla"

column_label: ROMAN_NUMERAL status
ROMAN_NUMERAL: /[ivxlcdm]+/
CAPITAL_ROMAN_NUMERAL: /[IVXLCDM]+/

surface_label: (OBVERSE | REVERSE | BOTTOM | EDGE | LEFT | RIGHT | TOP) status
OBVERSE: "o"
REVERSE:"r"
BOTTOM: "b.e."
EDGE: "e."
LEFT: "l.e."
RIGHT: "r.e."
TOP: "t.e."

?line_number: line_number_range 
      | single_line_number 
      | legacy_prefixed_single_line_number
line_number_range: single_line_number "-" single_line_number
single_line_number: [LETTER "+"] INT [PRIME | LEGACY_PRIME] [LETTER]
legacy_prefixed_single_line_number.-1: LETTER INT [PRIME | LEGACY_PRIME] [LETTER]

status: (PRIME | LEGACY_PRIME | UNCERTAIN | CORRECTION | COLLATION | NO_LONGER_VISIBLE)*
PRIME: "'"
LEGACY_PRIME: "′" | "’" | "ʾ"
UNCERTAIN: "?"
CORRECTION: "!"
COLLATION: "*"
NO_LONGER_VISIBLE: "°"